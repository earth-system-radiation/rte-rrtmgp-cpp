/*
 * This file is part of a C++ interface to the Radiative Transfer for Energetics (RTE)
 * and Rapid Radiative Transfer Model for GCM applications Parallel (RRTMGP).
 *
 * The original code is found at https://github.com/earth-system-radiation/rte-rrtmgp.
 *
 * Contacts: Robert Pincus and Eli Mlawer
 * email: rrtmgp@aer.com
 *
 * Copyright 2015-2020,  Atmospheric and Environmental Research and
 * Regents of the University of Colorado.  All right reserved.
 *
 * This C++ interface can be downloaded from https://github.com/earth-system-radiation/rte-rrtmgp-cpp
 *
 * Contact: Chiel van Heerwaarden
 * email: chiel.vanheerwaarden@wur.nl
 *
 * Copyright 2020, Wageningen University & Research.
 *
 * Use and duplication is permitted under the terms of the
 * BSD 3-clause license, see http://opensource.org/licenses/BSD-3-Clause
 *
 */
#include <chrono>
#include <numeric>
#include <iomanip>
#include <cmath>
#include <boost/algorithm/string.hpp>

#include "Optical_props.h"
#include "Source_functions.h"

#include "Gas_concs.h"
#include "Gas_optics_rrtmgp.h"
#include "Array.h"
#include "iostream"

#include "rrtmgp_kernel_launcher_cuda.h"

namespace
{
    template<typename TF>__global__
    void spread_col_kernel(
            const int ncol, const int ngpt, TF* __restrict__ src_out, const TF* __restrict__ src_in)
    {
        const int icol = blockIdx.x*blockDim.x + threadIdx.x;
        const int igpt = blockIdx.y*blockDim.y + threadIdx.y;

        if ( ( icol < ncol) && (igpt < ngpt) )
        {
            const int idx = icol + igpt*ncol;
            src_out[idx] = src_in[igpt];
        }
    }

    template<typename TF>
    void spread_col(
            const int ncol, const int ngpt, Array_gpu<TF,2>& src_out, const Array_gpu<TF,1>& src_in)
    {
        const int block_col = 16;
        const int block_gpt = 16;
        const int grid_col = ncol/block_col + (ncol%block_col > 0);
        const int grid_gpt = ngpt/block_gpt + (ngpt%block_gpt > 0);

        dim3 grid_gpu(grid_col, grid_gpt);
        dim3 block_gpu(block_col, block_gpt);

        spread_col_kernel<<<grid_gpu, block_gpu>>>(
            ncol, ngpt, src_out.ptr(), src_in.ptr());
    }

    int find_index(
            const Array<std::string,1>& data, const std::string& value)
    {
        auto it = std::find(data.v().begin(), data.v().end(), value);
        if (it == data.v().end())
            return -1;
        else
            return it - data.v().begin() + 1;
    }

    template<typename TF>
    void reduce_minor_arrays(
                const Gas_concs_gpu& available_gases,
                const Array<std::string,1>& gas_names,
                const Array<std::string,1>& gas_minor,
                const Array<std::string,1>& identifier_minor,
                const Array<TF,3>& kminor_atm,
                const Array<std::string,1>& minor_gases_atm,
                const Array<int,2>& minor_limits_gpt_atm,
                const Array<BOOL_TYPE,1>& minor_scales_with_density_atm,
                const Array<std::string,1>& scaling_gas_atm,
                const Array<BOOL_TYPE,1>& scale_by_complement_atm,
                const Array<int,1>& kminor_start_atm,

                Array<TF,3>& kminor_atm_red,
                Array<std::string,1>& minor_gases_atm_red,
                Array<int,2>& minor_limits_gpt_atm_red,
                Array<BOOL_TYPE,1>& minor_scales_with_density_atm_red,
                Array<std::string,1>& scaling_gas_atm_red,
                Array<BOOL_TYPE,1>& scale_by_complement_atm_red,
                Array<int,1>& kminor_start_atm_red)
    {
        int nm = minor_gases_atm.dim(1);
        int tot_g = 0;

        Array<BOOL_TYPE,1> gas_is_present({nm});

        for (int i=1; i<=nm; ++i)
        {
            const int idx_mnr = find_index(identifier_minor, minor_gases_atm({i}));

            // Search for
            std::string gas_minor_trimmed = gas_minor({idx_mnr});
            boost::trim(gas_minor_trimmed);

            gas_is_present({i}) = available_gases.exists(gas_minor_trimmed);
            if (gas_is_present({i}))
                tot_g += minor_limits_gpt_atm({2,i}) - minor_limits_gpt_atm({1,i}) + 1;
        }

        const int red_nm = std::accumulate(gas_is_present.v().begin(), gas_is_present.v().end(), 0);

        Array<TF,3> kminor_atm_red_t;

        if (red_nm == nm)
        {
            kminor_atm_red_t = kminor_atm;
            minor_gases_atm_red = minor_gases_atm;
            minor_limits_gpt_atm_red = minor_limits_gpt_atm;
            minor_scales_with_density_atm_red = minor_scales_with_density_atm;
            scaling_gas_atm_red = scaling_gas_atm;
            scale_by_complement_atm_red = scale_by_complement_atm;
            kminor_start_atm_red = kminor_start_atm;
        }
        else
        {
            // Use a lambda function as the operation has to be repeated many times.
            auto resize_and_set = [&](auto& a_red, const auto& a)
            {
                a_red.set_dims({red_nm});
                int counter = 1;
                for (int i=1; i<=gas_is_present.dim(1); ++i)
                {
                    if (gas_is_present({i}))
                    {
                       a_red({counter}) = a({i});
                       ++counter;
                    }
                }
            };

            resize_and_set(minor_gases_atm_red, minor_gases_atm);
            resize_and_set(minor_scales_with_density_atm_red, minor_scales_with_density_atm);
            resize_and_set(scaling_gas_atm_red, scaling_gas_atm);
            resize_and_set(scale_by_complement_atm_red, scale_by_complement_atm);
            resize_and_set(kminor_start_atm_red, kminor_start_atm);

            minor_limits_gpt_atm_red.set_dims({2, red_nm});
            kminor_atm_red_t.set_dims({tot_g, kminor_atm.dim(2), kminor_atm.dim(3)});

            int icnt = 0;
            int n_elim = 0;
            for (int i=1; i<=nm; ++i)
            {
                int ng = minor_limits_gpt_atm({2,i}) - minor_limits_gpt_atm({1,i}) + 1;
                if (gas_is_present({i}))
                {
                    ++icnt;
                    minor_limits_gpt_atm_red({1,icnt}) = minor_limits_gpt_atm({1,i});
                    minor_limits_gpt_atm_red({2,icnt}) = minor_limits_gpt_atm({2,i});
                    kminor_start_atm_red({icnt}) = kminor_start_atm({i}) - n_elim;

                    for (int j=1; j<=ng; ++j)
                        for (int i2=1; i2<=kminor_atm.dim(2); ++i2)
                            for (int i3=1; i3<=kminor_atm.dim(3); ++i3)
                                kminor_atm_red_t({kminor_start_atm_red({icnt})+j-1,i2,i3}) =
                                        kminor_atm({kminor_start_atm({i})+j-1,i2,i3});
                }
                else
                    n_elim += ng;
            }
        }

        // Reshape following the new ordering in v1.5.
        kminor_atm_red.set_dims({kminor_atm_red_t.dim(3), kminor_atm_red_t.dim(2), kminor_atm_red_t.dim(1)});
        for (int i3=1; i3<=kminor_atm_red.dim(3); ++i3)
            for (int i2=1; i2<=kminor_atm_red.dim(2); ++i2)
                for (int i1=1; i1<=kminor_atm_red.dim(1); ++i1)
                    kminor_atm_red({i1, i2, i3}) = kminor_atm_red_t({i3, i2, i1});
    }

    void create_idx_minor(
            const Array<std::string,1>& gas_names,
            const Array<std::string,1>& gas_minor,
            const Array<std::string,1>& identifier_minor,
            const Array<std::string,1>& minor_gases_atm,
            Array<int,1>& idx_minor_atm)
    {
        Array<int,1> idx_minor_atm_out({minor_gases_atm.dim(1)});

        for (int imnr=1; imnr<=minor_gases_atm.dim(1); ++imnr)
        {
            // Find identifying string for minor species in list of possible identifiers (e.g. h2o_slf)
            const int idx_mnr = find_index(identifier_minor, minor_gases_atm({imnr}));

            // Find name of gas associated with minor species identifier (e.g. h2o)
            idx_minor_atm_out({imnr}) = find_index(gas_names, gas_minor({idx_mnr}));
        }

        idx_minor_atm = idx_minor_atm_out;
    }

    void create_idx_minor_scaling(
            const Array<std::string,1>& gas_names,
            const Array<std::string,1>& scaling_gas_atm,
            Array<int,1>& idx_minor_scaling_atm)
    {
        Array<int,1> idx_minor_scaling_atm_out({scaling_gas_atm.dim(1)});

        for (int imnr=1; imnr<=scaling_gas_atm.dim(1); ++imnr)
            idx_minor_scaling_atm_out({imnr}) = find_index(gas_names, scaling_gas_atm({imnr}));

        idx_minor_scaling_atm = idx_minor_scaling_atm_out;
    }

    void create_key_species_reduce(
            const Array<std::string,1>& gas_names,
            const Array<std::string,1>& gas_names_red,
            const Array<int,3>& key_species,
            Array<int,3>& key_species_red,
            Array<BOOL_TYPE,1>& key_species_present_init)
    {
        const int np = key_species.dim(1);
        const int na = key_species.dim(2);
        const int nt = key_species.dim(3);

        key_species_red.set_dims({key_species.dim(1), key_species.dim(2), key_species.dim(3)});
        key_species_present_init.set_dims({gas_names.dim(1)});

        for (int i=1; i<=key_species_present_init.dim(1); ++i)
            key_species_present_init({i}) = 1;

        for (int ip=1; ip<=np; ++ip)
            for (int ia=1; ia<=na; ++ia)
                for (int it=1; it<=nt; ++it)
                {
                    const int ks = key_species({ip,ia,it});
                    if (ks != 0)
                    {
                        const int ksr = find_index(gas_names_red, gas_names({ks}));
                        key_species_red({ip,ia,it}) = ksr;
                        if (ksr == -1)
                            key_species_present_init({ks}) = 0;
                    }
                    else
                        key_species_red({ip,ia,it}) = ks;
                }
    }

    void check_key_species_present_init(
            const Array<std::string,1>& gas_names,
            const Array<BOOL_TYPE,1>& key_species_present_init
            )
    {
        for (int i=1; i<=key_species_present_init.dim(1); ++i)
        {
            if (key_species_present_init({i}) == 0)
            {
                std::string error_message = "Gas optics: required gas " + gas_names({i}) + " is missing";
                throw std::runtime_error(error_message);
            }
        }
    }

    void create_flavor(
            const Array<int,3>& key_species,
            Array<int,2>& flavor)
    {
        Array<int,2> key_species_list({2, key_species.dim(3)*2});

        // Prepare list of key species.
        int i = 1;
        for (int ibnd=1; ibnd<=key_species.dim(3); ++ibnd)
            for (int iatm=1; iatm<=key_species.dim(2); ++iatm)
            {
                key_species_list({1,i}) = key_species({1,iatm,ibnd});
                key_species_list({2,i}) = key_species({2,iatm,ibnd});
                ++i;
            }

        // Rewrite single key_species pairs.
        for (int i=1; i<=key_species_list.dim(2); ++i)
        {
            if ( key_species_list({1,i}) == 0 && key_species_list({2,i}) == 0 )
            {
                key_species_list({1,i}) = 2;
                key_species_list({2,i}) = 2;
            }
        }

        // Count unique key species pairs.
        int iflavor = 0;
        for (int i=1; i<=key_species_list.dim(2); ++i)
        {
            bool pair_exists = false;
            for (int ii=1; ii<=i-1; ++ii)
            {
                if ( (key_species_list({1,i}) == key_species_list({1,ii})) &&
                     (key_species_list({2,i}) == key_species_list({2,ii})) )
                {
                    pair_exists = true;
                    break;
                }
            }
            if (!pair_exists)
                ++iflavor;
        }

        // Fill flavors.
        flavor.set_dims({2,iflavor});
        iflavor = 0;
        for (int i=1; i<=key_species_list.dim(2); ++i)
        {
            bool pair_exists = false;
            for (int ii=1; ii<=i-1; ++ii)
            {
                if ( (key_species_list({1,i}) == key_species_list({1,ii})) &&
                     (key_species_list({2,i}) == key_species_list({2,ii})) )
                {
                    pair_exists = true;
                    break;
                }
            }
            if (!pair_exists)
            {
                ++iflavor;
                flavor({1,iflavor}) = key_species_list({1,i});
                flavor({2,iflavor}) = key_species_list({2,i});
            }
        }
    }

    int key_species_pair2flavor(
            const Array<int,2>& flavor,
            const Array<int,1>& key_species_pair)
    {
        // Search for match.
        for (int iflav=1; iflav<=flavor.dim(2); ++iflav)
        {
            if ( key_species_pair({1}) == flavor({1, iflav}) &&
                 key_species_pair({2}) == flavor({2, iflav}) )
                return iflav;
        }

        // No match found.
        return -1;
    }

    void create_gpoint_flavor(
            const Array<int,3>& key_species,
            const Array<int,1>& gpt2band,
            const Array<int,2>& flavor,
            Array<int,2>& gpoint_flavor)
    {
        const int ngpt = gpt2band.dim(1);
        gpoint_flavor.set_dims({2,ngpt});

        for (int igpt=1; igpt<=ngpt; ++igpt)
            for (int iatm=1; iatm<=2; ++iatm)
            {
                int pair_1 = key_species( {1, iatm, gpt2band({igpt})} );
                int pair_2 = key_species( {2, iatm, gpt2band({igpt})} );

                // Rewrite species pair.
                Array<int,1> rewritten_pair({2});
                if (pair_1 == 0 && pair_2 == 0)
                {
                    rewritten_pair({1}) = 2;
                    rewritten_pair({2}) = 2;
                }
                else
                {
                    rewritten_pair({1}) = pair_1;
                    rewritten_pair({2}) = pair_2;
                }

                // Write the output.
                gpoint_flavor({iatm,igpt}) = key_species_pair2flavor(
                        flavor, rewritten_pair);
            }
    }

    template<typename TF> __global__
    void fill_gases_kernel(
            const int ncol, const int nlay, const int dim1, const int dim2, const int ngas, const int igas,
            TF* __restrict__ vmr_out, const TF* __restrict__ vmr_in,
            TF* __restrict__ col_gas, const TF* __restrict__ col_dry)
    {
        const int icol = blockIdx.x*blockDim.x + threadIdx.x;
        const int ilay = blockIdx.y*blockDim.y + threadIdx.y;

        if ( (icol < ncol) && (ilay < nlay) )
        {
            const int idx_in = icol + ilay*ncol;
            const int idx_out1 = icol + ilay*ncol + (igas-1)*ncol*nlay;
            const int idx_out2 = icol + ilay*ncol + igas*ncol*nlay;

            if (igas > 0)
            {
                if (dim1 == 1 && dim2 == 1)
                     vmr_out[idx_out1] = vmr_in[0];
                else if (dim1 == 1)
                     vmr_out[idx_out1] = vmr_in[ilay];
                else
                    vmr_out[idx_out1] = vmr_in[idx_in];
                col_gas[idx_out2] = vmr_out[idx_out1] * col_dry[idx_in];
            }
            else if (igas == 0)
            {
                col_gas[idx_out2] = col_dry[idx_in];
            }
        }
    }
}

// Constructor of longwave variant.
template<typename TF>
Gas_optics_rrtmgp_gpu<TF>::Gas_optics_rrtmgp_gpu(
        const Gas_concs_gpu& available_gases,
        const Array<std::string,1>& gas_names,
        const Array<int,3>& key_species,
        const Array<int,2>& band2gpt,
        const Array<TF,2>& band_lims_wavenum,
        const Array<TF,1>& press_ref,
        const TF press_ref_trop,
        const Array<TF,1>& temp_ref,
        const TF temp_ref_p,
        const TF temp_ref_t,
        const Array<TF,3>& vmr_ref,
        const Array<TF,4>& kmajor,
        const Array<TF,3>& kminor_lower,
        const Array<TF,3>& kminor_upper,
        const Array<std::string,1>& gas_minor,
        const Array<std::string,1>& identifier_minor,
        const Array<std::string,1>& minor_gases_lower,
        const Array<std::string,1>& minor_gases_upper,
        const Array<int,2>& minor_limits_gpt_lower,
        const Array<int,2>& minor_limits_gpt_upper,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_lower,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_upper,
        const Array<std::string,1>& scaling_gas_lower,
        const Array<std::string,1>& scaling_gas_upper,
        const Array<BOOL_TYPE,1>& scale_by_complement_lower,
        const Array<BOOL_TYPE,1>& scale_by_complement_upper,
        const Array<int,1>& kminor_start_lower,
        const Array<int,1>& kminor_start_upper,
        const Array<TF,2>& totplnk,
        const Array<TF,4>& planck_frac,
        const Array<TF,3>& rayl_lower,
        const Array<TF,3>& rayl_upper) :
            Gas_optics_gpu<TF>(band_lims_wavenum, band2gpt),
            totplnk(totplnk)
{
    // Reshaping according to new dimension ordering since v1.5
    this->planck_frac.set_dims({planck_frac.dim(4), planck_frac.dim(2), planck_frac.dim(3), planck_frac.dim(1)});
    for (int i4=1; i4<=this->planck_frac.dim(4); ++i4)
        for (int i3=1; i3<=this->planck_frac.dim(3); ++i3)
            for (int i2=1; i2<=this->planck_frac.dim(2); ++i2)
                for (int i1=1; i1<=this->planck_frac.dim(1); ++i1)
                    this->planck_frac({i1, i2, i3, i4}) = planck_frac({i4, i2, i3, i1});

    // Initialize the absorption coefficient array, including Rayleigh scattering
    // tables if provided.
    init_abs_coeffs(
            available_gases,
            gas_names, key_species,
            band2gpt, band_lims_wavenum,
            press_ref, temp_ref,
            press_ref_trop, temp_ref_p, temp_ref_t,
            vmr_ref,
            kmajor, kminor_lower, kminor_upper,
            gas_minor,identifier_minor,
            minor_gases_lower, minor_gases_upper,
            minor_limits_gpt_lower,
            minor_limits_gpt_upper,
            minor_scales_with_density_lower,
            minor_scales_with_density_upper,
            scaling_gas_lower, scaling_gas_upper,
            scale_by_complement_lower,
            scale_by_complement_upper,
            kminor_start_lower,
            kminor_start_upper,
            rayl_lower, rayl_upper);

    // Temperature steps for Planck function interpolation.
    // Assumes that temperature minimum and max are the same for the absorption coefficient grid and the
    // Planck grid and the Planck grid is equally spaced.
    totplnk_delta = (temp_ref_max - temp_ref_min) / (totplnk.dim(1)-1);
}


// Constructor of the shortwave variant.
template<typename TF>
Gas_optics_rrtmgp_gpu<TF>::Gas_optics_rrtmgp_gpu(
        const Gas_concs_gpu& available_gases,
        const Array<std::string,1>& gas_names,
        const Array<int,3>& key_species,
        const Array<int,2>& band2gpt,
        const Array<TF,2>& band_lims_wavenum,
        const Array<TF,1>& press_ref,
        const TF press_ref_trop,
        const Array<TF,1>& temp_ref,
        const TF temp_ref_p,
        const TF temp_ref_t,
        const Array<TF,3>& vmr_ref,
        const Array<TF,4>& kmajor,
        const Array<TF,3>& kminor_lower,
        const Array<TF,3>& kminor_upper,
        const Array<std::string,1>& gas_minor,
        const Array<std::string,1>& identifier_minor,
        const Array<std::string,1>& minor_gases_lower,
        const Array<std::string,1>& minor_gases_upper,
        const Array<int,2>& minor_limits_gpt_lower,
        const Array<int,2>& minor_limits_gpt_upper,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_lower,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_upper,
        const Array<std::string,1>& scaling_gas_lower,
        const Array<std::string,1>& scaling_gas_upper,
        const Array<BOOL_TYPE,1>& scale_by_complement_lower,
        const Array<BOOL_TYPE,1>& scale_by_complement_upper,
        const Array<int,1>& kminor_start_lower,
        const Array<int,1>& kminor_start_upper,
        const Array<TF,1>& solar_source_quiet,
        const Array<TF,1>& solar_source_facular,
        const Array<TF,1>& solar_source_sunspot,
        const TF tsi_default,
        const TF mg_default,
        const TF sb_default,
        const Array<TF,3>& rayl_lower,
        const Array<TF,3>& rayl_upper) :
            Gas_optics_gpu<TF>(band_lims_wavenum, band2gpt)
{
    // Initialize the absorption coefficient array, including Rayleigh scattering
    // tables if provided.
    init_abs_coeffs(
            available_gases,
            gas_names, key_species,
            band2gpt, band_lims_wavenum,
            press_ref, temp_ref,
            press_ref_trop, temp_ref_p, temp_ref_t,
            vmr_ref,
            kmajor, kminor_lower, kminor_upper,
            gas_minor,identifier_minor,
            minor_gases_lower, minor_gases_upper,
            minor_limits_gpt_lower,
            minor_limits_gpt_upper,
            minor_scales_with_density_lower,
            minor_scales_with_density_upper,
            scaling_gas_lower, scaling_gas_upper,
            scale_by_complement_lower,
            scale_by_complement_upper,
            kminor_start_lower,
            kminor_start_upper,
            rayl_lower, rayl_upper);

    // Compute the solar source.
    this->solar_source_quiet = solar_source_quiet;
    this->solar_source_facular = solar_source_facular;
    this->solar_source_sunspot = solar_source_sunspot;

    this->solar_source.set_dims(solar_source_quiet.get_dims());

    set_solar_variability(mg_default, sb_default);
}


template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::init_abs_coeffs(
        const Gas_concs_gpu& available_gases,
        const Array<std::string,1>& gas_names,
        const Array<int,3>& key_species,
        const Array<int,2>& band2gpt,
        const Array<TF,2>& band_lims_wavenum,
        const Array<TF,1>& press_ref,
        const Array<TF,1>& temp_ref,
        const TF press_ref_trop,
        const TF temp_ref_p,
        const TF temp_ref_t,
        const Array<TF,3>& vmr_ref,
        const Array<TF,4>& kmajor,
        const Array<TF,3>& kminor_lower,
        const Array<TF,3>& kminor_upper,
        const Array<std::string,1>& gas_minor,
        const Array<std::string,1>& identifier_minor,
        const Array<std::string,1>& minor_gases_lower,
        const Array<std::string,1>& minor_gases_upper,
        const Array<int,2>& minor_limits_gpt_lower,
        const Array<int,2>& minor_limits_gpt_upper,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_lower,
        const Array<BOOL_TYPE,1>& minor_scales_with_density_upper,
        const Array<std::string,1>& scaling_gas_lower,
        const Array<std::string,1>& scaling_gas_upper,
        const Array<BOOL_TYPE,1>& scale_by_complement_lower,
        const Array<BOOL_TYPE,1>& scale_by_complement_upper,
        const Array<int,1>& kminor_start_lower,
        const Array<int,1>& kminor_start_upper,
        const Array<TF,3>& rayl_lower,
        const Array<TF,3>& rayl_upper)
{
    // Which gases known to the gas optics are present in the host model (available_gases)?
    std::vector<std::string> gas_names_to_use;

    for (const std::string &s : gas_names.v())
    {
        if (available_gases.exists(s))
            gas_names_to_use.push_back(s);
    }

    // Now the number of gases is the union of those known to the k-distribution and provided
    // by the host model.
    const int n_gas = gas_names_to_use.size();
    Array<std::string,1> gas_names_this(std::move(gas_names_to_use), {n_gas});
    this->gas_names = gas_names_this;

    // Initialize the gas optics object, keeping only those gases known to the
    // gas optics and also present in the host model.
    // Add an offset to the indexing to interface the negative ranging of fortran.
    Array<TF,3> vmr_ref_red({vmr_ref.dim(1), n_gas + 1, vmr_ref.dim(3)});
    vmr_ref_red.set_offsets({0, -1, 0});

    // Gas 0 is used in single-key species method, set to 1.0 (col_dry)
    for (int i1=1; i1<=vmr_ref_red.dim(1); ++i1)
        for (int i3=1; i3<=vmr_ref_red.dim(3); ++i3)
            vmr_ref_red({i1, 0, i3}) = vmr_ref({i1, 1, i3});

    for (int i=1; i<=n_gas; ++i)
    {
        int idx = find_index(gas_names, this->gas_names({i}));
        for (int i1=1; i1<=vmr_ref_red.dim(1); ++i1)
            for (int i3=1; i3<=vmr_ref_red.dim(3); ++i3)
                vmr_ref_red({i1, i, i3}) = vmr_ref({i1, idx+1, i3}); // CvH: why +1?
    }

    this->vmr_ref = std::move(vmr_ref_red);

    // Reduce minor arrays so variables only contain minor gases that are available.
    // Reduce size of minor Arrays.
    Array<std::string, 1> minor_gases_lower_red;
    Array<std::string, 1> scaling_gas_lower_red;
    Array<std::string, 1> minor_gases_upper_red;
    Array<std::string, 1> scaling_gas_upper_red;

    reduce_minor_arrays(
            available_gases,
            gas_names,
            gas_minor, identifier_minor,
            kminor_lower,
            minor_gases_lower,
            minor_limits_gpt_lower,
            minor_scales_with_density_lower,
            scaling_gas_lower,
            scale_by_complement_lower,
            kminor_start_lower,
            this->kminor_lower,
            minor_gases_lower_red,
            this->minor_limits_gpt_lower,
            this->minor_scales_with_density_lower,
            scaling_gas_lower_red,
            this->scale_by_complement_lower,
            this->kminor_start_lower);

    reduce_minor_arrays(
            available_gases,
            gas_names,
            gas_minor,
            identifier_minor,
            kminor_upper,
            minor_gases_upper,
            minor_limits_gpt_upper,
            minor_scales_with_density_upper,
            scaling_gas_upper,
            scale_by_complement_upper,
            kminor_start_upper,
            this->kminor_upper,
            minor_gases_upper_red,
            this->minor_limits_gpt_upper,
            this->minor_scales_with_density_upper,
            scaling_gas_upper_red,
            this->scale_by_complement_upper,
            this->kminor_start_upper);

    // Arrays not reduced by the presence, or lack thereof, of a gas
    this->press_ref = press_ref;
    this->temp_ref = temp_ref;

    // Reshaping according to new dimension ordering since v1.5
    this->kmajor.set_dims({kmajor.dim(4), kmajor.dim(2), kmajor.dim(3), kmajor.dim(1)});
    for (int i4=1; i4<=this->kmajor.dim(4); ++i4)
        for (int i3=1; i3<=this->kmajor.dim(3); ++i3)
            for (int i2=1; i2<=this->kmajor.dim(2); ++i2)
                for (int i1=1; i1<=this->kmajor.dim(1); ++i1)
                    this->kmajor({i1, i2, i3, i4}) = kmajor({i4, i2, i3, i1});

    // Reshaping according to new 1.5 release.
    // Create a new vector that consists of rayl_lower and rayl_upper stored in one variable.
    if (rayl_lower.size() > 0)
    {
        this->krayl.set_dims({rayl_lower.dim(3), rayl_lower.dim(2), rayl_lower.dim(1), 2});
        for (int i3=1; i3<=this->krayl.dim(3); ++i3)
            for (int i2=1; i2<=this->krayl.dim(2); ++i2)
                for (int i1=1; i1<=this->krayl.dim(1); ++i1)
                {
                    this->krayl({i1, i2, i3, 1}) = rayl_lower({i3, i2, i1});
                    this->krayl({i1, i2, i3, 2}) = rayl_upper({i3, i2, i1});
                }
    }

    // ---- post processing ----
    //  creates log reference pressure
    this->press_ref_log = this->press_ref;
    for (int i1=1; i1<=this->press_ref_log.dim(1); ++i1)
        this->press_ref_log({i1}) = std::log(this->press_ref_log({i1}));

    // log scale of reference pressure
    this->press_ref_trop_log = std::log(press_ref_trop);

    // Get index of gas (if present) for determining col_gas
    create_idx_minor(
            this->gas_names, gas_minor, identifier_minor, minor_gases_lower_red, this->idx_minor_lower);
    create_idx_minor(
            this->gas_names, gas_minor, identifier_minor, minor_gases_upper_red, this->idx_minor_upper);

    // Get index of gas (if present) that has special treatment in density scaling
    create_idx_minor_scaling(
            this->gas_names, scaling_gas_lower_red, this->idx_minor_scaling_lower);
    create_idx_minor_scaling(
            this->gas_names, scaling_gas_upper_red, this->idx_minor_scaling_upper);

    // Create flavor list.
    // Reduce (remap) key_species list; checks that all key gases are present in incoming
    Array<int,3> key_species_red;
    Array<BOOL_TYPE,1> key_species_present_init;

    create_key_species_reduce(
            gas_names, this->gas_names, key_species, key_species_red, key_species_present_init);

    check_key_species_present_init(gas_names, key_species_present_init);

    // create flavor list
    create_flavor(key_species_red, this->flavor);

    // create gpoint flavor list
    create_gpoint_flavor(
            key_species_red, this->get_gpoint_bands(), this->flavor, this->gpoint_flavor);

    // minimum, maximum reference temperature, pressure -- assumes low-to-high ordering
    // for T, high-to-low ordering for p
    this->temp_ref_min = this->temp_ref({1});
    this->temp_ref_max = this->temp_ref({temp_ref.dim(1)});
    this->press_ref_min = this->press_ref({press_ref.dim(1)});
    this->press_ref_max = this->press_ref({1});

    // creates press_ref_log, temp_ref_delta
    this->press_ref_log_delta =
            (std::log(this->press_ref_min) - std::log(this->press_ref_max)) / (this->press_ref.dim(1) - 1);
    this->temp_ref_delta = (this->temp_ref_max - this->temp_ref_min) / (this->temp_ref.dim(1) - 1);

    // Which species are key in one or more bands?
    // this->flavor is an index into this->gas_names
    // if (allocated(this%is_key)) deallocate(this%is_key) ! Shouldn't ever happen...
    Array<int,1> is_key({get_ngas()}); // CvH bool, defaults to 0.?

    for (int j=1; j<=this->flavor.dim(2); ++j)
        for (int i=1; i<=this->flavor.dim(1); ++i)
        {
            if (this->flavor({i, j}) != 0)
                is_key({this->flavor({i, j})}) = true;
        }

    this->is_key = is_key;

    // copy arrays to gpu
    this->press_ref_log_gpu = this->press_ref_log;
    this->temp_ref_gpu = this->temp_ref;
    this->vmr_ref_gpu = this->vmr_ref;
    this->flavor_gpu = this->flavor;
    this->gpoint_flavor_gpu = this->gpoint_flavor;
    this->kmajor_gpu = this->kmajor;
    this->krayl_gpu = this->krayl;
    this->kminor_lower_gpu = this->kminor_lower;
    this->kminor_upper_gpu = this->kminor_upper;
    this->minor_limits_gpt_lower_gpu = this->minor_limits_gpt_lower;
    this->minor_limits_gpt_upper_gpu = this->minor_limits_gpt_upper;
    this->minor_scales_with_density_lower_gpu = this->minor_scales_with_density_lower;
    this->minor_scales_with_density_upper_gpu = this->minor_scales_with_density_upper;
    this->scale_by_complement_lower_gpu = this->scale_by_complement_lower;
    this->scale_by_complement_upper_gpu = this->scale_by_complement_upper;
    this->idx_minor_lower_gpu = this->idx_minor_lower;
    this->idx_minor_upper_gpu = this->idx_minor_upper;
    this->idx_minor_scaling_lower_gpu = this->idx_minor_scaling_lower;
    this->idx_minor_scaling_upper_gpu = this->idx_minor_scaling_upper;
    this->kminor_start_lower_gpu = this->kminor_start_lower;
    this->kminor_start_upper_gpu = this->kminor_start_upper;
    this->totplnk_gpu = this->totplnk;
    this->planck_frac_gpu = this->planck_frac;
}





template<typename TF> __global__
void compute_delta_plev(
        const int ncol, const int nlay,
        const TF* __restrict__ plev,
        TF* __restrict__ delta_plev)
{
    const int icol = blockIdx.x*blockDim.x + threadIdx.x;
    const int ilay = blockIdx.y*blockDim.y + threadIdx.y;

    if ( (icol < ncol) && (ilay < nlay) )
    {
        const int idx = icol + ilay*ncol;

        delta_plev[idx] = abs(plev[idx] - plev[idx + ncol]);

        // delta_plev({icol, ilay}) = std::abs(plev({icol, ilay}) - plev({icol, ilay+1}));
    }
}


template<typename TF> __global__
void compute_m_air(
        const int ncol, const int nlay,
        const TF* __restrict__ vmr_h2o,
        TF* __restrict__ m_air)
{
    const int icol = blockIdx.x*blockDim.x + threadIdx.x;
    const int ilay = blockIdx.y*blockDim.y + threadIdx.y;

    constexpr TF m_dry = 0.028964;
    constexpr TF m_h2o = 0.018016;

    if ( (icol < ncol) && (ilay < nlay) )
    {
        const int idx = icol + ilay*ncol;

        m_air[idx] = (m_dry + m_h2o * vmr_h2o[idx]) / (TF(1.) + vmr_h2o[idx]);
    }
}


template<typename TF> __global__
void compute_col_dry(
        const int ncol, const int nlay,
        const TF* __restrict__ delta_plev, const TF* __restrict__ m_air, const TF* __restrict__ vmr_h2o,
        TF* __restrict__ col_dry)
{
    const int icol = blockIdx.x*blockDim.x + threadIdx.x;
    const int ilay = blockIdx.y*blockDim.y + threadIdx.y;

    constexpr TF g0 = 9.80665;
    constexpr TF avogad = 6.02214076e23;

    if ( (icol < ncol) && (ilay < nlay) )
    {
        const int idx = icol + ilay*ncol;

        col_dry[idx] = TF(10.) * delta_plev[idx] * avogad / (TF(1000.)*m_air[idx]*TF(100.)*g0);
        col_dry[idx] /= (TF(1.) + vmr_h2o[idx]);
    }
}


// Calculate the molecules of dry air.
template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::get_col_dry(
        Array_gpu<TF,2>& col_dry, const Array_gpu<TF,2>& vmr_h2o,
        const Array_gpu<TF,2>& plev)
{
    Array_gpu<TF,2> delta_plev({col_dry.dim(1), col_dry.dim(2)});
    Array_gpu<TF,2> m_air     ({col_dry.dim(1), col_dry.dim(2)});

    const int block_lay = 16;
    const int block_col = 16;

    const int nlay = col_dry.dim(2);
    const int ncol = col_dry.dim(1);

    const int grid_col = ncol/block_col + (ncol%block_col > 0);
    const int grid_lay = nlay/block_lay + (nlay%block_lay > 0);

    dim3 grid_gpu(grid_col, grid_lay);
    dim3 block_gpu(block_col, block_lay);

    compute_delta_plev<<<grid_gpu, block_gpu>>>(
            ncol, nlay,
            plev.ptr(),
            delta_plev.ptr());

    compute_m_air<<<grid_gpu, block_gpu>>>(
            ncol, nlay,
            vmr_h2o.ptr(),
            m_air.ptr());

    compute_col_dry<<<grid_gpu, block_gpu>>>(
            ncol, nlay,
            delta_plev.ptr(), m_air.ptr(), vmr_h2o.ptr(),
            col_dry.ptr());
}


// Gas optics solver longwave variant.
template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::gas_optics(
        const Array_gpu<TF,2>& play,
        const Array_gpu<TF,2>& plev,
        const Array_gpu<TF,2>& tlay,
        const Array_gpu<TF,1>& tsfc,
        const Gas_concs_gpu& gas_desc,
        std::unique_ptr<Optical_props_arry_gpu<TF>>& optical_props,
        Source_func_lw_gpu<TF>& sources,
        const Array_gpu<TF,2>& col_dry,
        const Array_gpu<TF,2>& tlev)
{
    const int ncol = play.dim(1);
    const int nlay = play.dim(2);
    const int ngpt = this->get_ngpt();
    const int nband = this->get_nband();

    Array_gpu<int,2> jtemp({play.dim(1), play.dim(2)});
    Array_gpu<int,2> jpress({play.dim(1), play.dim(2)});
    Array_gpu<BOOL_TYPE,2> tropo({play.dim(1), play.dim(2)});
    Array_gpu<TF,6> fmajor({2, 2, 2, play.dim(1), play.dim(2), this->get_nflav()});
    Array_gpu<int,4> jeta({2, play.dim(1), play.dim(2), this->get_nflav()});

    // Gas optics.
    compute_gas_taus(
            ncol, nlay, ngpt, nband,
            play, plev, tlay, gas_desc,
            optical_props,
            jtemp, jpress, jeta, tropo, fmajor,
            col_dry);

    // External sources.
    source(
            ncol, nlay, nband, ngpt,
            play, plev, tlay, tsfc,
            jtemp, jpress, jeta, tropo, fmajor,
            sources, tlev);
}


// Gas optics solver shortwave variant.
template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::gas_optics(
        const Array_gpu<TF,2>& play,
        const Array_gpu<TF,2>& plev,
        const Array_gpu<TF,2>& tlay,
        const Gas_concs_gpu& gas_desc,
        std::unique_ptr<Optical_props_arry_gpu<TF>>& optical_props,
        Array_gpu<TF,2>& toa_src,
        const Array_gpu<TF,2>& col_dry)
{
    const int ncol = play.dim(1);
    const int nlay = play.dim(2);
    const int ngpt = this->get_ngpt();
    const int nband = this->get_nband();

    Array_gpu<int,2> jtemp({play.dim(1), play.dim(2)});
    Array_gpu<int,2> jpress({play.dim(1), play.dim(2)});
    Array_gpu<BOOL_TYPE,2> tropo({play.dim(1), play.dim(2)});
    Array_gpu<TF,6> fmajor({2, 2, 2, play.dim(1), play.dim(2), this->get_nflav()});
    Array_gpu<int,4> jeta({2, play.dim(1), play.dim(2), this->get_nflav()});

    // Gas optics.
    compute_gas_taus(
            ncol, nlay, ngpt, nband,
            play, plev, tlay, gas_desc,
            optical_props,
            jtemp, jpress, jeta, tropo, fmajor,
            col_dry);

    // External source function is constant.
    spread_col(ncol, ngpt, toa_src, this->solar_source_gpu);
}


template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::compute_gas_taus(
        const int ncol, const int nlay, const int ngpt, const int nband,
        const Array_gpu<TF,2>& play,
        const Array_gpu<TF,2>& plev,
        const Array_gpu<TF,2>& tlay,
        const Gas_concs_gpu& gas_desc,
        std::unique_ptr<Optical_props_arry_gpu<TF>>& optical_props,
        Array_gpu<int,2>& jtemp, Array_gpu<int,2>& jpress,
        Array_gpu<int,4>& jeta,
        Array_gpu<BOOL_TYPE,2>& tropo,
        Array_gpu<TF,6>& fmajor,
        const Array_gpu<TF,2>& col_dry)
{
    Array_gpu<TF,3> tau({ngpt, nlay, ncol});
    Array_gpu<TF,3> tau_rayleigh({ngpt, nlay, ncol});
    Array_gpu<TF,3> vmr({ncol, nlay, this->get_ngas()});
    Array_gpu<TF,3> col_gas({ncol, nlay, this->get_ngas()+1});
    col_gas.set_offsets({0, 0, -1});
    Array_gpu<TF,4> col_mix({2, ncol, nlay, this->get_nflav()});
    Array_gpu<TF,5> fminor({2, 2, ncol, nlay, this->get_nflav()});


    // CvH add all the checking...
    const int ngas = this->get_ngas();
    const int nflav = this->get_nflav();
    const int neta = this->get_neta();
    const int npres = this->get_npres();
    const int ntemp = this->get_ntemp();

    const int nminorlower = this->minor_scales_with_density_lower.dim(1);
    const int nminorklower = this->kminor_lower.dim(3);
    const int nminorupper = this->minor_scales_with_density_upper.dim(1);
    const int nminorkupper = this->kminor_upper.dim(3);

    const int block_lay = 16;
    const int block_col = 16;

    const int grid_col = ncol/block_col + (ncol%block_col > 0);
    const int grid_lay = nlay/block_lay + (nlay%block_lay > 0);

    dim3 grid_gpu(grid_col, grid_lay);
    dim3 block_gpu(block_col, block_lay);

    for (int igas=0; igas<=ngas; ++igas)
    {
        const Array_gpu<TF,2>& vmr_2d = igas > 0 ? gas_desc.get_vmr(this->gas_names({igas})) : gas_desc.get_vmr(this->gas_names({1}));
        fill_gases_kernel<<<grid_gpu, block_gpu>>>(
            ncol, nlay, vmr_2d.dim(1), vmr_2d.dim(2), ngas, igas, vmr.ptr(), vmr_2d.ptr(), col_gas.ptr(), col_dry.ptr());
    }

    // rrtmgp_kernel_launcher_cuda::zero_array(ngpt, nlay, ncol, tau);

    rrtmgp_kernel_launcher_cuda::interpolation(
            ncol, nlay,
            ngas, nflav, neta, npres, ntemp,
            flavor_gpu,
            press_ref_log_gpu,
            temp_ref_gpu,
            this->press_ref_log_delta,
            this->temp_ref_min,
            this->temp_ref_delta,
            this->press_ref_trop_log,
            vmr_ref_gpu,
            play,
            tlay,
            col_gas,
            jtemp,
            fmajor, fminor,
            col_mix,
            tropo,
            jeta, jpress);

    int idx_h2o = -1;
    for  (int i=1; i<=this->gas_names.dim(1); ++i)
        if (gas_names({i}) == "h2o")
        {
            idx_h2o = i;
            break;
        }

    if (idx_h2o == -1)
        throw std::runtime_error("idx_h2o cannot be found");

    bool has_rayleigh = (this->krayl.size() > 0);

    if (has_rayleigh)
    {
        Array_gpu<TF,3> tau({ncol, nlay, ngpt});
        Array_gpu<TF,3> tau_rayleigh({ncol, nlay, ngpt});
        rrtmgp_kernel_launcher_cuda::zero_array(ngpt, nlay, ncol, tau);

        rrtmgp_kernel_launcher_cuda::zero_array(ncol, nlay, ngpt, tau);
        rrtmgp_kernel_launcher_cuda::zero_array(ncol, nlay, ngpt, tau_rayleigh);

        rrtmgp_kernel_launcher_cuda::compute_tau_absorption(
                ncol, nlay, nband, ngpt,
                ngas, nflav, neta, npres, ntemp,
                nminorlower, nminorklower,
                nminorupper, nminorkupper,
                idx_h2o,
                gpoint_flavor_gpu,
                this->get_band_lims_gpoint(),
                kmajor_gpu,
                kminor_lower_gpu,
                kminor_upper_gpu,
                minor_limits_gpt_lower_gpu,
                minor_limits_gpt_upper_gpu,
                minor_scales_with_density_lower_gpu,
                minor_scales_with_density_upper_gpu,
                scale_by_complement_lower_gpu,
                scale_by_complement_upper_gpu,
                idx_minor_lower_gpu,
                idx_minor_upper_gpu,
                idx_minor_scaling_lower_gpu,
                idx_minor_scaling_upper_gpu,
                kminor_start_lower_gpu,
                kminor_start_upper_gpu,
                tropo,
                col_mix, fmajor, fminor,
                play, tlay, col_gas,
                jeta, jtemp, jpress,
                tau,
                static_cast<void*>(this));

        rrtmgp_kernel_launcher_cuda::compute_tau_rayleigh(
                ncol, nlay, nband, ngpt,
                ngas, nflav, neta, npres, ntemp,
                gpoint_flavor_gpu,
                this->get_gpoint_bands_gpu(),
                this->get_band_lims_gpoint(),
                krayl_gpu,
                idx_h2o, col_dry, col_gas,
                fminor, jeta, tropo, jtemp,
                tau_rayleigh,
                static_cast<void*>(this));

        combine_abs_and_rayleigh(tau, tau_rayleigh, optical_props);
    }
    else
    {
        rrtmgp_kernel_launcher_cuda::zero_array(ncol, nlay, ngpt, optical_props->get_tau());

        rrtmgp_kernel_launcher_cuda::compute_tau_absorption(
                ncol, nlay, nband, ngpt,
                ngas, nflav, neta, npres, ntemp,
                nminorlower, nminorklower,
                nminorupper, nminorkupper,
                idx_h2o,
                gpoint_flavor_gpu,
                this->get_band_lims_gpoint(),
                kmajor_gpu,
                kminor_lower_gpu,
                kminor_upper_gpu,
                minor_limits_gpt_lower_gpu,
                minor_limits_gpt_upper_gpu,
                minor_scales_with_density_lower_gpu,
                minor_scales_with_density_upper_gpu,
                scale_by_complement_lower_gpu,
                scale_by_complement_upper_gpu,
                idx_minor_lower_gpu,
                idx_minor_upper_gpu,
                idx_minor_scaling_lower_gpu,
                idx_minor_scaling_upper_gpu,
                kminor_start_lower_gpu,
                kminor_start_upper_gpu,
                tropo,
                col_mix, fmajor, fminor,
                play, tlay, col_gas,
                jeta, jtemp, jpress,
                optical_props->get_tau(),
                static_cast<void*>(this));
    }
}


template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::combine_abs_and_rayleigh(
        const Array_gpu<TF,3>& tau,
        const Array_gpu<TF,3>& tau_rayleigh,
        std::unique_ptr<Optical_props_arry_gpu<TF>>& optical_props)
{
    int ncol = tau.dim(1);
    int nlay = tau.dim(2);
    int ngpt = tau.dim(3);

    rrtmgp_kernel_launcher_cuda::combine_abs_and_rayleigh(
            ncol, nlay, ngpt,
            tau, tau_rayleigh,
            optical_props->get_tau(), optical_props->get_ssa(), optical_props->get_g(),
            static_cast<void*>(this));
}


template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::source(
        const int ncol, const int nlay, const int nbnd, const int ngpt,
        const Array_gpu<TF,2>& play, const Array_gpu<TF,2>& plev,
        const Array_gpu<TF,2>& tlay, const Array_gpu<TF,1>& tsfc,
        const Array_gpu<int,2>& jtemp, const Array_gpu<int,2>& jpress,
        const Array_gpu<int,4>& jeta, const Array_gpu<BOOL_TYPE,2>& tropo,
        const Array_gpu<TF,6>& fmajor,
        Source_func_lw_gpu<TF>& sources,
        const Array_gpu<TF,2>& tlev)
{
    const int nflav = this->get_nflav();
    const int neta = this->get_neta();
    const int npres = this->get_npres();
    const int ntemp = this->get_ntemp();
    const int nPlanckTemp = this->get_nPlanckTemp();
    auto gpoint_bands = this->get_gpoint_bands_gpu();
    auto band_lims_gpoint = this->get_band_lims_gpoint_gpu();

    Array_gpu<TF,3> lay_source_t({ngpt, nlay, ncol});
    Array_gpu<TF,3> lev_source_inc_t({ngpt, nlay, ncol});
    Array_gpu<TF,3> lev_source_dec_t({ngpt, nlay, ncol});
    Array_gpu<TF,2> sfc_source_t({ngpt, ncol});
    Array_gpu<TF,2> sfc_source_jac({ngpt, ncol});

    int sfc_lay = play({1, 1}) > play({1, nlay}) ? 1 : nlay;

    rrtmgp_kernel_launcher_cuda::Planck_source(
            ncol, nlay, nbnd, ngpt,
            nflav, neta, npres, ntemp, nPlanckTemp,
            tlay, tlev, tsfc, sfc_lay,
            fmajor, jeta, tropo, jtemp, jpress,
            gpoint_bands, band_lims_gpoint, this->planck_frac_gpu, this->temp_ref_min,
            this->totplnk_delta, this->totplnk_gpu, this->gpoint_flavor_gpu,
            sources.get_sfc_source(), sources.get_lay_source(), sources.get_lev_source_inc(), 
            sources.get_lev_source_dec(), sources.get_sfc_source_jac(),
            static_cast<void*>(this));
}


template<typename TF>
void Gas_optics_rrtmgp_gpu<TF>::set_solar_variability(
        const TF mg_index, const TF sb_index)
{
    constexpr TF a_offset = TF(0.1495954);
    constexpr TF b_offset = TF(0.00066696);

    for (int igpt=1; igpt<=this->solar_source_quiet.dim(1); ++igpt)
    {
        this->solar_source({igpt}) = this->solar_source_quiet({igpt})
                + (mg_index - a_offset) * this->solar_source_facular({igpt})
                + (sb_index - b_offset) * this->solar_source_sunspot({igpt});
    }
    this->solar_source_gpu = this->solar_source;
}


template<typename TF>
TF Gas_optics_rrtmgp_gpu<TF>::get_tsi() const
{
    const int n_gpt = this->get_ngpt();

    TF tsi = 0.;
    for (int igpt=1; igpt<=n_gpt; ++igpt)
        tsi += this->solar_source({igpt});

    return tsi;
}


#ifdef RTE_RRTMGP_SINGLE_PRECISION
template class Gas_optics_rrtmgp_gpu<float>;
#else
template class Gas_optics_rrtmgp_gpu<double>;
#endif
